cmake_minimum_required(VERSION 3.16.3)

set(CMAKE_TOOLCHAIN_FILE ${CMAKE_CURRENT_LIST_DIR}/toolchain/tricore-gcc.cmake)
set(LINKER_SCRIPT ${CMAKE_CURRENT_LIST_DIR}/toolchain/Lcf_Gnuc_Tricore_Tc.lsl)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY bin)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_C_STANDARD 99)    # Mostly for LLD

project(scrutiny-aurix-tc334)

include(FetchContent)

set(SCRUTINY_ENABLE_DATALOGGING 1)
set(SCRUTINY_SUPPORT_64BITS 1) 
set(SCRUTINY_DATALOGGING_BUFFER_32BITS 0)

FetchContent_Declare(
  scrutiny-embedded
  GIT_REPOSITORY https://github.com/scrutinydebugger/scrutiny-embedded.git
  GIT_TAG        master
)
FetchContent_MakeAvailable(scrutiny-embedded)

add_compile_options(
    -g
    -gdwarf-4
)

add_subdirectory(bsp)   # Contains drivers and board configuration. Mostly auto generated by Aurix Development Studio
add_executable(${PROJECT_NAME} 
    src/main.cpp
    src/board.cpp
    src/task_controller.cpp
    src/scrutiny_integration.cpp
    src/wave_function_generator.cpp
    )
set_target_properties(${PROJECT_NAME} PROPERTIES SUFFIX ".elf")

target_include_directories(${PROJECT_NAME} PRIVATE 
    src
)

# Infineon iLLD uses of __asm macros in their headers triggers C++11 warnings when we include them in a cpp source. 
target_compile_options(${PROJECT_NAME} PRIVATE -Wno-literal-suffix) 

target_link_libraries(${PROJECT_NAME} PRIVATE 
    bsp
    scrutiny-embedded
    )

# Linker Script
target_link_options(${PROJECT_NAME} PRIVATE -T${LINKER_SCRIPT})
set_target_properties(${PROJECT_NAME} PROPERTIES LINK_DEPENDS ${LINKER_SCRIPT})
set_target_properties(${PROJECT_NAME} PROPERTIES LINK_FLAGS -Wl,-Map=${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${PROJECT_NAME}.map)

scrutiny_postbuild(${PROJECT_NAME} 
    TAGGED_EXECUTABLE_TARGET_VAR TAGGED_BIN_TARGET
    INSTALL_SFD
    
    METADATA_PROJECT_NAME "Aurix Demo"
    METADATA_VERSION "1.0.0"
    METADATA_AUTHOR "Scrutiny Debugger"
    ALIAS_FILES
        aliases/wavegen_alias.json
    CU_IGNORE_PATTERNS 
        */ignored_file.cpp      # We can skip one or many files to parse with a list of glob expression
    PATH_IGNORE_PATTERNS 
        *_impure_dat2as*        # We can skip one or many variables witha  lsit of glob expression

    CPPFILT ${CMAKE_CPPFILT}    # Make sure we use tricore-elf-c++filt for demangling 

)

get_target_property(TAGGED_BIN_FILE ${TAGGED_BIN_TARGET} TARGET_FILE)
get_filename_component(TAGGED_BIN_FILE_BASENAME ${TAGGED_BIN_FILE} NAME_WE)   # Name Without Extension

# Post builds 
# Produce the .hex
add_custom_command(TARGET ${TAGGED_BIN_TARGET}  POST_BUILD
     DEPENDS ${TAGGED_BIN_FILE}
     BYPRODUCTS ${TAGGED_BIN_FILE_BASENAME}.hex
     VERBATIM 
     COMMAND ${CMAKE_OBJCOPY} ARGS 
        -O ihex
        "${TAGGED_BIN_FILE}"
        "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${TAGGED_BIN_FILE_BASENAME}.hex"
     )

# Produce the .lst
add_custom_command(TARGET ${TAGGED_BIN_TARGET}  POST_BUILD
     DEPENDS ${TAGGED_BIN_FILE}
     BYPRODUCTS ${TAGGED_BIN_FILE_BASENAME}.lst
     VERBATIM 
     COMMAND ${CMAKE_OBJDUMP} ARGS
        -h -S 
        "${TAGGED_BIN_FILE}" > "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${TAGGED_BIN_FILE_BASENAME}.lst"
     )
